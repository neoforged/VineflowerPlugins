import net.neoforged.gradleutils.PomUtilsExtension

plugins {
    id 'signing'
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.gradleutils'
}

group = 'net.neoforged'
base {
    archivesName = 'vineflower-plugins'
}
gradleutils {
    version {
        branches {
            suffixBranch()
        }
    }
    // setupCentralPublishing()
    // setupSigning(signAllPublications: true)
}
version = gradleutils.version

changelog {
    from '0.1'
    // For fine-grained changelog publication control
    disableAutomaticPublicationRegistration()
}

repositories {
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.named('javadoc').configure {
    options.addBooleanOption('Xdoclint:all,-missing', true)
}

tasks.withType(JavaCompile).configureEach() {
    options.encoding = "UTF-8"
}

final javaComponent = (AdhocComponentWithVariants) project.components.getByName("java")

def vineflowerBundleClasspath
def vineflowerBundleElements
configurations {
    changelog {
        canBeResolved = false
        canBeConsumed = true
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.DOCUMENTATION))
            attribute(DocsType.DOCS_TYPE_ATTRIBUTE, objects.named(DocsType, "changelog"))
        }
        javaComponent.addVariantsFromConfiguration(it) {}
    }
    final vineflowerBundle = dependencyScope('vineflowerBundle')
    vineflowerBundleClasspath = resolvable('vineflowerBundleClasspath') {
        extendsFrom(vineflowerBundle.get())
    }
}

dependencies {
    implementation 'org.vineflower:vineflower:1.11.2'

    vineflowerBundle 'org.vineflower:vineflower:1.11.2'

    testImplementation platform('org.junit:junit-bom:5.13.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

abstract class MergeJars extends DefaultTask {
    @Classpath
    abstract ConfigurableFileCollection getInput();
    @OutputFile
    abstract RegularFileProperty getOutput();
    @TaskAction
    def merge() {
        // Find the right manifest
        final m = new java.util.jar.Manifest()
        m.mainAttributes[java.util.jar.Attributes.Name.MANIFEST_VERSION] = '1.0'
        input.forEach {
            it.newInputStream().withCloseable {
                new java.util.jar.JarInputStream(it).withCloseable { jin ->
                    if (jin.manifest) {
                        m.mainAttributes.putAll(jin.manifest.mainAttributes)
                        m.entries.putAll(jin.manifest.entries)
                    }
                }
            }
        }

        final dirsCreated = new java.util.HashSet<>()
        output.asFile.get().newOutputStream().withCloseable { out -> {
            new java.util.jar.JarOutputStream(out, m).withCloseable { jout ->
                input.forEach {
                    new java.util.jar.JarFile(it).withCloseable { jin ->
                        jin.entries().each { entry ->
                            if (entry.name == "META-INF/MANIFEST.MF") {
                                return // Handled separately
                            }
                            if (entry.isDirectory()) {
                                if (dirsCreated.add(entry.name)) {
                                    jout.putNextEntry(new java.util.jar.JarEntry(entry.name))
                                    jout.closeEntry()
                                }
                                return
                            }

                            jout.putNextEntry(new java.util.jar.JarEntry(entry.name))
                            jin.getInputStream(entry).withCloseable { entryIn ->
                                entryIn.transferTo(jout)
                            }
                            jout.closeEntry()
                        }
                    }
                }
            }
        }}
    }
}

final vineflowerBundleJar = tasks.register('vineflowerBundleJar', MergeJars.class) {
    group = 'build'
    input.from(vineflowerBundleClasspath)
    input.from(tasks.named('jar'))
    output = layout.buildDirectory.file('libs/vf-bundle.jar')
}

artifacts {
    // Only publish a changelog for releases
    changelog(createChangelog.outputFile) {
        builtBy(createChangelog)
        setClassifier('changelog')
        setExtension('txt')
    }
}

test {
    useJUnitPlatform()
}

// This is needed for the TestClass classes to test final parameter removal
tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ["-parameters"]
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'vineflower-plugins'
            from components.java
            artifact(vineflowerBundleJar) {
                classifier = 'vfbundle'
            }
            pom {
                name = 'VineflowerPlugins'
                description = 'Plugins for the VineFlower decompiler, allowing removal of final on method parameters'
                pomUtils.githubRepo(it, 'VineflowerPlugins')
                pomUtils.neoForgedDeveloper(it)
                pomUtils.license(it, PomUtilsExtension.License.LGPL_v2)
            }
        }
    }

    repositories {
        maven gradleutils.publishingMaven
    }
}
